# Deploy an application to any Cloud‚Ñ¢ VM with Terraform, Docker & cloud-init

[![GitHub tag (latest SemVer)](https://img.shields.io/github/v/tag/christippett/terraform-cloudinit-container-server?label=Version)](./CHANGELOG.md) [![Terraform Registry](https://img.shields.io/badge/Terraform-Registry-623CE4)](https://registry.terraform.io/modules/christippett/container-server/cloudinit/)

üôÖ‚Äç‚ôÄÔ∏è No external dependencies.
üè¥‚Äç‚ò†Ô∏è No proprietary frameworks.
‚õë No onerous configuration.

... just plain ol' `docker`, `docker-compose` and `systemd` ‚Äî deployed with `cloud-init` using a single, cloud-agnostic configuration script.

---

**Table of Contents**

<!-- START doctoc generated TOC please keep comment here to allow auto update -->
<!-- DON'T EDIT THIS SECTION, INSTEAD RE-RUN doctoc TO UPDATE -->

- [Features](#features)
- [Quickstart](#quickstart)
  - [1Ô∏è‚É£ Deploying a single container](#%E2%83%A3-deploying-a-single-container)
  - [2Ô∏è‚É£ Deploying a `docker-compose.yaml` file](#%E2%83%A3-deploying-a-docker-composeyaml-file)
- [Cloud‚Ñ¢Ô∏è Examples](#cloud-examples)
  - [‚òÅ AWS](#%E2%98%81-aws)
  - [‚òÅ Google Cloud](#%E2%98%81-google-cloud)
  - [‚òÅ Azure](#%E2%98%81-azure)
  - [‚òÅ DigitalOcean](#%E2%98%81-digitalocean)
- [Custom Configuration](#custom-configuration)
  - [Traefik](#traefik)
  - [Cloud-Init](#cloud-init)
- [FAQ](#faq)
  - [What's this cloud-init thing?](#whats-this-cloud-init-thing)
  - [What else do I need to know?](#what-else-do-i-need-to-know)
  - [Why use this over something like Fargate or Cloud Run?](#why-use-this-over-something-like-fargate-or-cloud-run)
- [Terraform Module](#terraform-module)
  - [Inputs](#inputs)
  - [Outputs](#outputs)

<!-- END doctoc generated TOC please keep comment here to allow auto update -->

---

## Features

- ‚òÅÔ∏è Deploy a single container image or `docker-compose.yaml` manifest to a VM running on any of the following clouds:
  - **AWS** ([see example](./examples/aws-docker-image-simple/))
  - **Google Cloud Platform** ([see example](./examples/gcp-docker-image-simple/))
  - **DigitalOcean** ([see example](./examples/digitalocean-docker-image-simple/))
  - **Azure** ([see example](./examples/azure-docker-image-simple))
  - _...and theoretically any other vendor that supports cloud-init_
- üì¶ Easy to deploy via **Terraform**.
- üåê Host multiple services on the same domain, with routing and service discovery provided by **Traefik**.
- üîë Automatic SSL/TLS certificates generated by **Let's Encrypt** (courtesy of Traefik üëÜ).
- üõ† Extremely customisable using environment variables or your own cloud-init config

## Quickstart

The output of this module is a [cloud-config](https://cloudinit.readthedocs.io/en/latest/topics/format.html#cloud-config-data) file that can be plugged directly into your instance's Terraform configuration, either as `user_data` (AWS / DigitalOcean), `metadata.user-data` (Google Cloud) or `custom_data` (Azure). When the instance first boots, cloud-init will do its thing and your containers will be running in no time.

### 1Ô∏è‚É£ Deploying a single container

```hcl
module "cloudinit" {
  source  = "christippett/container-server/cloudinit"
  version = "~> 2.0"

  domain = "example.com"
  image  = "nginx:latest" # üê≥ deploy a single container

  letsencrypt = {
    email  = "me@example.com"
    server = "staging" # / "prod"
  }

}
```

### 2Ô∏è‚É£ Deploying a `docker-compose.yaml` file

```hcl
module "cloudinit" {
  source  = "christippett/container-server/cloudinit"
  version = "~> 2.0"

  domain         = "example.com"
  docker_compose = file("docker-compose.yaml") # üê≥ deploy multiple services

  letsencrypt = {
    email  = "me@example.com"
    server = "staging" # / "prod"
  }

}
```

When supplying your own `docker-compose.yaml` configuration, Traefik will automatically route each service to its own subdomain following the pattern **`<service_name>.<domain>`**.

## Cloud‚Ñ¢Ô∏è Examples

The examples below demonstrate creating and deploying virtual machines from different cloud vendors using the output from this module.

### ‚òÅ AWS

```hcl
resource "aws_instance" "vm" {
  ami             = "ami-0560993025898e8e8" # Amazon Linux 2
  instance_type   = "t2.micro"
  security_groups = ["sg-allow-everything-from-anywhere"]

  user_data = module.cloudinit.cloud_config # üëà
}

```

### ‚òÅ Google Cloud

```hcl
resource "google_compute_instance" "vm" {
  name         = "my-server"
  project      = "my-project"
  zone         = "australia-southeast1"
  machine_type = "e2-small"
  tags         = ["http-server", "https-server"]

  metadata = {
    user-data = module.cloudinit.cloud_config # üëà
  }

  boot_disk {
    initialize_params {
      image = data.google_compute_image.cos.self_link
    }
  }

  network_interface {
    subnetwork         = "vpc"
    subnetwork_project = "my-project"
    access_config {}
  }
}

```

### ‚òÅ Azure

```hcl
resource "azurerm_linux_virtual_machine" "vm" {
  name                = "my-server"
  resource_group_name = azurerm_resource_group.example.name
  location            = azurerm_resource_group.example.location
  size                = "Standard_F2"
  admin_username      = "adminuser"

  custom_data = base64encode(module.cloudinit.cloud_config) # üëà

  network_interface_ids = [
    azurerm_network_interface.example.id,
  ]

  os_disk {
    caching              = "ReadWrite"
    storage_account_type = "Standard_LRS"
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "UbuntuServer"
    sku       = "20.04-LTS"
    version   = "latest"
  }
}

```

### ‚òÅ DigitalOcean

```hcl
resource "digitalocean_droplet" "vm" {
  name   = "my-server"
  image  = "docker-18-04"
  region = "lon1"
  size   = "s-1vcpu-1gb"

  user_data = module.cloudinit.cloud_config # üëà
}
```

## Custom Configuration

### Traefik

‚ö†Ô∏è `TODO: Describe customising Traefik through environment variables.`

> _Some info on Traefik's default config:_
>
> - üîó Traefik connects to services over the Docker network `traefik` ‚Äî any service(s) you want routed through Traefik need to be on this network.
> - üîí Let's Encrypt is enabled via the `letsencrypt` certificate resolver and by default uses the [HTTP challenge](https://doc.traefik.io/traefik/user-guides/docker-compose/acme-http/) method for validating domains.
> - üìã Traefik's static configuration is set through environment variables. You can override any value by providing your own environment variable definition via the `environment` input variable.
> - üìä By default the module enables Traefik's [dashboard](https://docs.traefik.io/operations/dashboard/) and API. Both are accessible from the `traefik` subdomain (e.g. `https://traefik.${domain}/dashboard/`) using the username/password: `admin`/`traefik`.

### Cloud-Init

‚ö†Ô∏è `TODO: Describe extending module through additional cloud-init steps.`

## FAQ

### What's this cloud-init thing?

This whole project started as an experiment into how `cloud-init` could be used to easily bootstrap and configure instances across different cloud providers. This is what Canonical has to say about it:

> _Cloud-init is the industry standard multi-distribution method for cross-platform cloud instance initialization. It is supported across all major public cloud providers, provisioning systems for private cloud infrastructure, and bare-metal installations._
>
> _Cloud-init will identify the cloud it is running on during boot, read any provided metadata from the cloud and initialize the system accordingly. This may involve setting up network and storage devices to configuring SSH access key and many other aspects of a system. Later on cloud-init will also parse and process any optional user or vendor data that was passed to the instance._
>
> Source: https://github.com/canonical/cloud-init

### What else do I need to know?

Whatever cloud platform you decide to go with, it's important that the base OS image includes both `docker` and `systemd`. This is the only hard requirement for things to _Just Work_, everything else that's needed is run as a container (e.g. Docker Compose). The following operating systems have been tested to work successfully:

- [Google's Container Optimized OS](https://cloud.google.com/container-optimized-os)
- [Amazon Linux 2](https://aws.amazon.com/amazon-linux-2/)
- [Ubuntu 18.04 (via DigitalOcean's Marketplace)](https://marketplace.digitalocean.com/apps/docker)

### Why use this over something like Fargate or Cloud Run?

Even the most basic and cheapest of VMs are capable of running _a lot_ of containers. As fantastic as the cloud's PaaS and serverless offerings are, it's sometimes easier to orchestrate several containers on one machine without having to mess around with IAM, networking, service inter-dependencies etc. Deploying several services with Docker Compose can be a cost-effective and simpler alternative when hosting small hobby projects, POCs or other experimental workloads.

To throw about some quick numbers, below are the going rates for a low-cost VM running on each of the major cloud platforms. These would be more than capable of running dozens of containers, especially if you don't expect them to receive much traffic.

- **AWS**
  - **Cost:** USD\$4.76/month<br />
    _t3a.micro ‚Ä¢ 2vCPU/1GB ‚Ä¢ 10GB HDD_
- **Google Cloud Platform**
  - **Cost:** USD\$6.11/month\*\*<br />
    _e2.micro ‚Ä¢ 0.25vCPU/1GB ‚Ä¢ 10GB HDD_
- **DigitalOcean**
  - **Cost:** USD\$6.00/month\*\*<br />
    _Standard Droplet ‚Ä¢ 1vCPU/1GB ‚Ä¢ 10 HDD_
- **Azure**
  - **Cost:** USD\$14.73/month\*\*<br />
    _A0 ‚Ä¢ 1vCPU/0.75GB ‚Ä¢ 32GB HDD_

# Terraform Module

## Inputs

| Name                | Description                                                                                                                    | Type                                                        | Default | Required |
| ------------------- | ------------------------------------------------------------------------------------------------------------------------------ | ----------------------------------------------------------- | ------- | :------: |
| domain              | The domain to deploy applications under.                                                                                       | `string`                                                    | n/a     |   yes    |
| email               | The email address used for requesting certificates from Lets Encrypt.                                                          | `string`                                                    | n/a     |   yes    |
| cloudinit_part      | Supplementary cloud-init config used to customise the instance.                                                                | `list(object({ content_type : string, content : string }))` | `[]`    |    no    |
| container           | The container definition used to deploy a Docker image to the server. Follows the same schema as a Docker Compose service.     | `any`                                                       | `{}`    |    no    |
| enable_webhook      | Flag whether to enable the webhook endpoint on the server, allowing updates to be made independent of Terraform.               | `bool`                                                      | `false` |    no    |
| env                 | A list environment variables provided as key/value pairs. These can be used to interpolate values within Docker Compsoe files. | `map(string)`                                               | `{}`    |    no    |
| files               | A list of files to upload to the server. Content must be base64 encoded. Files are available under the `/run/app/` directory.  | `list(object({ filename : string, content : string }))`     | `[]`    |    no    |
| letsencrypt_staging | Boolean flag to decide whether the Let's Encrypt staging server should be used.                                                | `bool`                                                      | `false` |    no    |

## Outputs

| Name                  | Description                                                      |
| --------------------- | ---------------------------------------------------------------- |
| cloud_config          | Content of the cloud-init config to be deployed to a server.     |
| docker_compose_config | Content of the Docker Compose config to be deployed to a server. |
| environment_variables | n/a                                                              |
| included_files        | n/a                                                              |
